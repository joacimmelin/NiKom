/* Mark Text command
$VER: Mark Text 1.1 (28.3.18)
N=MARK TEXT
#=502
A=#
*/

parse arg nodeId userId textId .

options results

'sendstring' '0d0a0a'x

filename = 'NiKom:Users/' || userId % 100 || '/' || userId || '/MarkedTexts'
if textId = '' then do
  'lasttext'
  textStr = RESULT
  if textStr = 'RESULT' then do
    'sendstring Du har inte läst någon text ännu!' '0d0a'x
     exit 0
  end

  if Abbrev(textStr, 'B') then do
    'sendstring Du kan inte markera brev' '0d0a'x
    exit 0
  end

  parse var textStr 'T' textId confId .
end

else do
  if ~datatype(textId, 'n') then do
    'sendstring Skriv: Markera Text <textnummer>' '0d0a'x
    'sendstring eller bara: Markera Text för att markera den senast lästa texten' '0d0a'x
    exit 0
  end
  
  confId = TextInfo(textId, 'm')
  if confId < 0 then do
    'sendstring Text' textId 'finns inte.' '0d0a'x
    exit 0
  end

  if MeetRight(userId, confId) ~= 1 then do
    'sendstring Du har inte rätt att markera text' textId '0d0a'x
    exit 0
  end

  textStr = 'T' textId confId
end

if Exists(filename) then
  mode = 'Append'
else
  mode = 'Write'
if ~Open('file', filename,  mode) then do
  'sendstring Kunde inte öppna' filename '0d0a'x
  exit 5
end
call WriteLn('file', textStr)
call Close('file')

'sendstring Text' textId 'i mötet' MeetInfo(confId, 'n') 'markerad.' '0d0a'x
