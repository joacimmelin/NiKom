/* Unread Marked command
$VER: Unread Marked 1.1 (28.3.18)
N=UNREAD MARKED
#=510
*/

parse arg nodeId userId flag .
options results

'sendstring' '0d0a0a'x

filename = 'NiKom:Users/' || userId % 100 || '/' || userId || '/MarkedTexts'

if ~Exists(filename) then do
  'sendstring Du har inga markerade texter.' '0d0a'x
  exit 0
end

if ~Open('file', filename, 'Read') then do
  'sendstring Kunde inte öppna' filename
  exit 5
end
do until Eof('file')
 line = ReadLn('file')
 if line = '' then iterate
 parse var line 'T' textId confId .
 if(MeetInfo(confId, 'y') ~= 0) then do
   'sendstring Kan inte oläsa texter i mötet' MeetInfo(confId, 'n') '0d0a'x
   iterate
 end
 res = MarkTextUnread(userId, textId)
 if res = 0 then
   'sendstring Oläser text' textId 'i mötet' MeetInfo(confId, 'n') '0d0a'x
 else if res = -3 then
    'sendstring Text' textId 'i mötet' MeetInfo(confId, 'n') 'är redan oläst' '0d0a'x
 else
   'sendstring Kunde inte oläsa text' textId 'i mötet' MeetInfo(confId, 'n') '0d0a'x
end
Close('file')

if flag = '-p' then do
  address command 'delete' filename
  'sendstring Raderar markerade texter-filen.' '0d0a'x
end
